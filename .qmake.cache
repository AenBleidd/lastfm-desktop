win32: ROOT_DIR = $$system( cd )
else:  ROOT_DIR = $$system( pwd )

macx-xcode {
    BUILD_DIR = build
} else {
    # With Xcode we generate a bundle
    BUILD_DIR = _build
    DESTDIR = $$ROOT_DIR/_bin
    LIBS += -L$$DESTDIR
}

OBJECTS_DIR = $$BUILD_DIR
MOC_DIR = $$BUILD_DIR
UI_DIR = $$BUILD_DIR
RCC_DIR = $$BUILD_DIR
INCLUDEPATH += $$ROOT_DIR

win32:DEFINES += _CRT_SECURE_NO_WARNINGS WIN32_LEAN_AND_MEAN

# allow use of and, or, etc. as symbols
unix:QMAKE_CXXFLAGS = $$QMAKE_CXXFLAGS -fno-operator-names

macx* {
	QMAKE_PKGINFO_TYPEINFO = last
	QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.4
}

CONFIG( debug, debug|release ):CONFIG -= release

release:INSTALL_DIR = $$(DESTDIR)$$(PREFIX)
else:INSTALL_DIR = prefix
